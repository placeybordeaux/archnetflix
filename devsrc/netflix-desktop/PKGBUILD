# Maintainer: Jesus Alvarez <jeezusjr@gmail.com>
# Contributor: sxe <sxxe@gmx.de>
# Based on the wine-git in AUR

pkgname='netflix-desktop'
pkgver=0.2.5
pkgrel=1
pkgdesc="An automated script for viewing Netflix through Firefox and patched WINE"
url="https://aur.archlinux.org/packages/netflix-desktop/"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('wine-silverlight' 'ttf-ms-fonts' 'zenity' 'wget')
if [[ "$CARCH" == "x86_64" ]]; then
  depends+=(
    'lib32-libsm' 'lib32-alsa-lib' 'lib32-openal' 'lib32-mpg123'
    'lib32-libpng12' 'lib32-libpng' 'multilib-devel'
  )
  optdepends=(
    'lib32-ati-dri: for open source ATI driver users'
    'lib32-catalyst-utils: for AMD Catalyst users'
    'lib32-intel-dri: for open source Intel driver users'
    'lib32-nouveau-dri: for Nouveau users'
    'lib32-nvidia-utils-bumblebee: for NVIDIA + Bumblebee users'
    'lib32-nvidia-utils: for NVIDIA proprietary blob users'
    'xorg-xset: for screensaver/DPMS toggling'
  )
else
  depends+=(
    'libsm' 'libpng12' 'libpng' 'alsa-lib' 'openal' 'mpg123' 'base-devel'
  )
fi
install=netflix-desktop.install

source=("netflix-desktop"
        "netflix-desktop.desktop"
        "install-file-downloader"
        "prefs.js"
        "localstore.rdf"
        "Makefile"
        "test-xattr.c"
        "NetflixIcon.png::http://developer.netflix.com/files/Netflix_API2_57x57.png"
        "filesums")

build() {
  cd "$srcdir"
  make
}

package() {
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/netflix-desktop"
  mkdir -p "$pkgdir/usr/share/applications"
  mkdir -p "$pkgdir/usr/share/netflix-desktop/netflix-profile"
  install -m644 $srcdir/filesums $pkgdir/usr/share/netflix-desktop/filesums
  install -m755 $srcdir/test-xattr $pkgdir/usr/share/netflix-desktop
  install -m644 $srcdir/localstore.rdf $pkgdir/usr/share/netflix-desktop/netflix-profile
  install -m644 $srcdir/prefs.js $pkgdir/usr/share/netflix-desktop/netflix-profile
  install -m755 $srcdir/netflix-desktop $pkgdir/usr/bin/netflix-desktop
  install -m644 $srcdir/NetflixIcon.png $pkgdir/usr/share/netflix-desktop/NetflixIcon.png
  install -m644 $srcdir/netflix-desktop.desktop $pkgdir/usr/share/applications/netflix-desktop.desktop
  install -m755 $srcdir/install-file-downloader $pkgdir/usr/share/netflix-desktop/install-file-downloader
}

md5sums=('81ff5fb598dd61b46da4571195d55d38'
         'a41f098487affa1de34c8b44d52617b5'
         '64b5f99b9074f90ab8ec404a149ed1ba'
         '4b185f403ecf4ea16ac0b2962ee2dca6'
         'a46caea4a34a088d59e9760ac9df13ed'
         '80de72740312c37e3927d50f6895162f'
         'cdd64a466ee699c567bb0ca23b7a8811'
         '0a4c2ef50831a751dae74315eb066c01'
         'b9f9949cf9470f1cc55086e629605325')

# vim:set ts=2 sw=2 et:
